apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.shenhua.outer.security.report"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
                GETUI_APP_ID    : "8vkU6qtMEH97OOr23PJJH8",
                GETUI_APP_KEY   : "hhdO5laSnD8oSNSwXpBFb2",
                GETUI_APP_SECRET: "AJXvyhff3Q77ulOcpVW95"
        ]

        multiDexEnabled true

//        ndk {
//            abiFilters "armeabi", "armeabi-v7a", "x86_64"
//        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        beta {
            buildConfigField "int", "ENV_TYPE", "1"
            applicationId 'com.shenhua.outer.security.report.beta'
            manifestPlaceholders = [
                    app_name: "安防监控-beta"
            ]
        }
        rc {
            buildConfigField "int", "ENV_TYPE", "2"
            applicationId 'com.shenhua.outer.security.report.rc'
            manifestPlaceholders = [
                    app_name: "安防监控-rc"
            ]
        }
        rel {
            buildConfigField "int", "ENV_TYPE", "0"
            applicationId 'com.shenhua.outer.security.report'
            manifestPlaceholders = [
                    app_name: "安防监控"
            ]
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            File file = new File(getProjectDir().parent + File.separator + "apks")
            if (!file.exists()) {
                file.mkdir();
            }
            def fileName = outputFile.name.replace("app", "SecurityReport"
                    + "-V${defaultConfig.versionName}")
            output.outputFile = new File(file, fileName)
        }
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.1'
    compile 'com.jakewharton:butterknife:8.8.1'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.3.0'
    compile 'com.getui:sdk:2.11.1.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile project(':calendar')
}
